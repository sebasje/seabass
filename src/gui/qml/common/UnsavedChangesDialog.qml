import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import SeabassGui

// Blocks leaving a page (or the app) with staged changes. Only two ways
// out, by design: save them or throw them away.
SeabassDialog {
    id: dialog
    property string message: "You have made changes to your library. Please save these changes to your library first."
    property string saveText: "Save Changes"
    property string discardText: "Discard Changes"
    property int pendingCount: 0

    signal saveRequested()
    signal discardRequested()

    severity: SeabassDialog.Warning
    closePolicy: Popup.NoAutoClose
    title: "Unsaved changes"
    headline: dialog.message
    detailText: dialog.pendingCount > 0
        ? dialog.pendingCount + " change(s) are staged and not on the stick yet." : ""

    footer: DialogButtonBox {
        Button {
            objectName: "saveButton"
            text: dialog.saveText
            highlighted: true
            DialogButtonBox.buttonRole: DialogButtonBox.AcceptRole
        }
        Button {
            objectName: "discardButton"
            text: dialog.discardText
            DialogButtonBox.buttonRole: DialogButtonBox.DestructiveRole
            onClicked: {
                dialog.close();
                dialog.discardRequested();
            }
        }
    }
    onAccepted: dialog.saveRequested()
}
